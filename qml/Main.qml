pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Layouts
import QtQuick.Controls.FluentWinUI3
import QtQuick.Controls.impl
import QtQuick.Window
import ChrisLauinger77.QontrolPanel
import Qt.labs.platform as Platform

ApplicationWindow {
    id: panel
    visible: false
    flags: Qt.FramelessWindowHint | Qt.WindowStaysOnTopHint
    color: "#00000000"
    width: 360
    height: Math.max(1, Math.min(preferredPanelHeight(), maximumPanelHeight()))

    property bool isAnimatingIn: false
    property bool isAnimatingOut: false
    property bool nativeBackdropActive: false
    property real restingX: 0
    property real restingY: 0
    property real mediaRestingX: 0
    property real mediaRestingY: 0
    property bool pendingShowGeometryUpdate: false
    property bool suppressNextTrayShow: false
    property int showAnimationGeneration: 0
    property alias mediaSurfaceWindow: mediaPanelWindow
    readonly property bool chatMixEffectiveEnabled: UserSettings.activateChatmix && UserSettings.chatMixEnabled
    // Preserve the original 24px spacer after the media content's 15px outer inset.
    readonly property real panelGap: 9
    property string taskbarPos: {
        switch (UserSettings.panelPosition) {
            case 0: return "top";
            case 1: return "bottom";
            case 2: return "left";
            case 3: return "right";
            default: return "bottom";
        }
    }
    property var targetScreenGeometry: ({ x: 0, y: 0, width: Utils.getAvailableDesktopWidth(), height: Utils.getAvailableDesktopHeight() })

    function refreshTargetScreenGeometry() {
        const geometry = Utils.getCursorScreenAvailableGeometry()
        targetScreenGeometry = {
            x: geometry.x,
            y: geometry.y,
            width: geometry.width,
            height: geometry.height
        }
    }

    function maximumPanelHeight() {
        let reservedHeight = UserSettings.yAxisMargin
        if (mediaPanelWindow.available) {
            reservedHeight += mediaPanelWindow.height + panelGap
        }
        return Math.max(1, targetScreenGeometry.height - reservedHeight)
    }

    function preferredPanelHeight() {
        return contentFlickable.contentHeight
    }

    onVisibleChanged: {
        if (!visible) {
            if (UserSettings.showAudioLevel) {
                AudioBridge.stopAudioLevelMonitoring()
                AudioBridge.stopApplicationAudioLevelMonitoring()
            }
        } else {
            if (UserSettings.showAudioLevel) {
                AudioBridge.startAudioLevelMonitoring()
                AudioBridge.startApplicationAudioLevelMonitoring()
            }
            if (UserSettings.allowBrightnessControl) {
                MonitorManager.refreshMonitors()
                brightnessSlider.value = MonitorManager.brightness
            }
        }
    }

    onClosing: function(close) {
        if (visible) {
            close.accepted = false
            hidePanel()
        }
    }

    Component.onCompleted: {
        Utils.setStyle(UserSettings.panelStyle)
        Qt.callLater(updateNativeBackdrop)
    }

    function updateNativeBackdrop() {
        nativeBackdropActive = WindowsBackdrop.applyTransientBackdrop(panel)
    }

    Connections {
        target: Qt.application.styleHints

        function onColorSchemeChanged() {
            panel.updateNativeBackdrop()
        }
    }

    PowerConfirmationWindow {
        id: powerConfirmationWindow
    }

    Connections {
        target: KeyboardShortcutManager

        function onPanelToggleRequested() {
            panel.togglePanel()
        }

        function onMicMuteToggleRequested() {
            AudioBridge.setInputMute(!AudioBridge.inputMuted)
        }
    }

    Connections {
        target: UserSettings
        function onPanelPositionChanged() {
            if (panel.visible) {
                panel.repositionWindows()
            }
        }

        function onXAxisMarginChanged() {
            if (panel.visible) {
                panel.repositionWindows()
            }
        }

        function onYAxisMarginChanged() {
            if (panel.visible) {
                panel.repositionWindows()
            }
        }

        function onPanelAnimationsEnabledChanged() {
            if (panel.isAnimatingOut || hideAnimation.running) {
                panel.hidePanelImmediately()
            } else if (panel.visible) {
                panel.showPanelImmediately(false)
            }
        }
    }

    Connections {
        target: AudioBridge
        function onSaveFailed(message) {
            systemTray.showMessage(qsTr("Settings could not be saved"), message)
        }
        function onOutputDeviceCountChanged() {
            if (AudioBridge.outputDevices.count <= 1) {
                outputDevicesRect.expanded = false
            }
        }
        function onInputDeviceCountChanged() {
            if (AudioBridge.inputDevices.count <= 1) {
                inputDevicesRect.expanded = false
            }
        }
    }

    IntroWindow {
        id: introWindow
        Component.onCompleted: {
            if (UserSettings.firstRun) {
                Qt.callLater(function() {
                    showIntro()
                    panel.showPanel()
                })
            }
        }
    }

    SystemTray {
        id: systemTray
        onTogglePanelRequested: {
            // Losing focus may already have started hiding the panel before
            // the tray activation reaches QML.
            if (panel.visible) {
                trayToggleTimer.stop()
                if (panel.suppressNextTrayShow) {
                    focusLossTrayGuardTimer.stop()
                    panel.suppressNextTrayShow = false
                } else if (!panel.isAnimatingOut) {
                    panel.hidePanel()
                }
            } else if (panel.suppressNextTrayShow) {
                focusLossTrayGuardTimer.stop()
                panel.suppressNextTrayShow = false
            } else {
                trayToggleTimer.restart()
            }
        }
        onSettingsWindowRequested: {
            trayToggleTimer.stop()
            settingsWindow.showPreferredPane()
        }
    }

    Timer {
        id: trayToggleTimer
        interval: 200
        repeat: false
        onTriggered: {
            if (!panel.visible) {
                HeadsetControlBridge.refreshNow()
                panel.showPanel()
            }
        }
    }

    Timer {
        id: focusLossTrayGuardTimer
        // Keep the marker beyond the 300 ms hide so a delayed tray activation
        // cannot reinterpret the completed hide as a request to show again.
        interval: 500
        repeat: false
        onTriggered: panel.suppressNextTrayShow = false
    }

    Shortcut {
        sequences: [StandardKey.Cancel]
        onActivated: {
            if (!panel.isAnimatingOut && panel.visible) {
                panel.hidePanel()
            }
        }
    }

    ChatMixNotification {}

    MediaOverlay {}

    MainMediaWindow {
        id: mediaPanelWindow
        transientParent: panel

        onAvailableChanged: panel.handleMediaAvailabilityChanged()
        onHideRequested: panel.hidePanel()
    }

    Timer {
        id: contentOpacityTimer
        interval: 160
        repeat: false
        onTriggered: mainLayout.opacity = 1
    }

    Timer {
        id: flyoutOpacityTimer
        interval: 160
        repeat: false
        onTriggered: mediaPanelWindow.contentOpacity = 1
    }

    onHeightChanged: {
        if (visible) {
            repositionWindows()
        }
    }

    ParallelAnimation {
        id: showAnimation

        PropertyAnimation {
            id: mainShowAnimation
            target: panel
            duration: 300
            easing.type: Easing.OutCubic
        }

        PropertyAnimation {
            id: mediaShowAnimation
            target: mediaPanelWindow
            duration: 300
            easing.type: Easing.OutCubic
        }

        onStarted: {
            contentOpacityTimer.start()
            flyoutOpacityTimer.start()
        }
        onFinished: {
            panel.isAnimatingIn = false
            // Moving a hidden OpenGL window on-screen does not always schedule a frame.
            panel.update()
        }
    }

    ParallelAnimation {
        id: hideAnimation

        PropertyAnimation {
            id: mainHideAnimation
            target: panel
            duration: 300
            easing.type: Easing.InCubic
        }

        PropertyAnimation {
            id: mediaHideAnimation
            target: mediaPanelWindow
            duration: 300
            easing.type: Easing.InCubic
        }

        onFinished: {
            panel.visible = false
            mediaPanelWindow.visible = false
            panel.isAnimatingOut = false
            panel.resetWindowPositions()
        }
    }

    function animationProperty() {
        return panel.taskbarPos === "left" || panel.taskbarPos === "right" ? "x" : "y"
    }

    function showAnimationProgress() {
        const currentPosition = mainShowAnimation.property === "x" ? panel.x : panel.y
        const totalDistance = Math.abs(mainShowAnimation.to - mainShowAnimation.from)
        if (totalDistance <= 0) {
            return 1
        }

        return Math.max(0, Math.min(1,
                                    1 - Math.abs(mainShowAnimation.to - currentPosition) / totalDistance))
    }

    function togglePanel() {
        if (!UserSettings.panelAnimationsEnabled) {
            if (visible) {
                hidePanel()
            } else {
                showPanel()
            }
            return
        }

        if (isAnimatingOut) {
            return
        }

        if (isAnimatingIn) {
            showAnimation.stop()
            isAnimatingIn = false
            closeAllMenusAndCollapse()

            const adjustedDuration = Math.max(50, Math.round(showAnimationProgress() * 300))
            setHideDuration(adjustedDuration)
            startHideAnimation()
            return
        }

        if (visible) {
            hidePanel()
        } else {
            showPanel()
        }
    }

    function showPanel() {
        focusLossTrayGuardTimer.stop()
        suppressNextTrayShow = false

        if (!UserSettings.panelAnimationsEnabled) {
            showPanelImmediately(true)
            return
        }

        if (isAnimatingIn || isAnimatingOut) {
            return
        }

        isAnimatingIn = true
        const generation = ++showAnimationGeneration
        pendingShowGeometryUpdate = false

        // A hidden native window can retain its last on-screen backing-store
        // frame while Qt applies the new off-screen position. Keep the whole
        // surface transparent until the staged geometry passes have completed.
        panel.opacity = 0
        mediaPanelWindow.opacity = 0
        positionWindowsAtTarget(true)
        setInitialWindowPositions()

        mediaPanelWindow.visible = mediaPanelWindow.available
        panel.visible = true
        panel.requestActivate()

        Qt.callLater(function() {
            if (generation !== panel.showAnimationGeneration
                    || !panel.isAnimatingIn || !UserSettings.panelAnimationsEnabled) {
                return
            }

            Qt.callLater(function() {
                if (generation !== panel.showAnimationGeneration
                        || !panel.isAnimatingIn || !UserSettings.panelAnimationsEnabled) {
                    return
                }

                positionWindowsAtTarget()
                setInitialWindowPositions()
                pendingShowGeometryUpdate = false

                Qt.callLater(function() {
                    if (generation !== panel.showAnimationGeneration
                            || !panel.isAnimatingIn || !UserSettings.panelAnimationsEnabled) {
                        return
                    }

                    if (panel.pendingShowGeometryUpdate) {
                        panel.positionWindowsAtTarget()
                        panel.setInitialWindowPositions()
                        panel.pendingShowGeometryUpdate = false
                    }
                    panel.opacity = 1
                    mediaPanelWindow.opacity = 1
                    panel.startAnimation()
                })
            })
        })
    }

    function clearPanelAnimationState() {
        ++showAnimationGeneration
        showAnimation.stop()
        hideAnimation.stop()
        contentOpacityTimer.stop()
        flyoutOpacityTimer.stop()
        mainOpacityAnimation.stop()
        mediaPanelWindow.finishContentOpacityAnimation()
        pendingShowGeometryUpdate = false
        isAnimatingIn = false
        isAnimatingOut = false
        panel.opacity = 1
        mediaPanelWindow.opacity = 1
        mainLayout.opacity = 1
        mediaPanelWindow.contentOpacity = 1
    }

    function showPanelImmediately(activatePanel) {
        clearPanelAnimationState()
        positionWindowsAtTarget(true)
        mediaPanelWindow.visible = mediaPanelWindow.available
        panel.visible = true
        if (activatePanel) {
            panel.requestActivate()
        }
    }

    function hidePanelImmediately() {
        clearPanelAnimationState()
        mediaPanelWindow.visible = false
        panel.visible = false
        closeAllMenusAndCollapse()
        positionWindowsAtTarget()
    }

    function positionWindowsAtTarget(refreshGeometry) {
        if (refreshGeometry) {
            refreshTargetScreenGeometry()
        }

        const screenX = targetScreenGeometry.x
        const screenY = targetScreenGeometry.y
        const screenWidth = targetScreenGeometry.width
        const screenHeight = targetScreenGeometry.height

        const marginX = UserSettings.xAxisMargin
        const marginY = UserSettings.yAxisMargin

        const targetX = panel.taskbarPos === "left"
                ? screenX + marginX
                : screenX + screenWidth - panel.width - marginX
        restingX = Math.max(screenX,
                            Math.min(targetX, screenX + screenWidth - panel.width))
        restingY = panel.taskbarPos === "top"
                ? screenY + marginY
                  + (mediaPanelWindow.available ? mediaPanelWindow.height + panelGap : 0)
                : screenY + screenHeight - panel.height - marginY

        mediaRestingX = restingX
        mediaRestingY = panel.taskbarPos === "top"
                ? screenY + marginY
                : restingY - mediaPanelWindow.height - panelGap

        resetWindowPositions()
    }

    function resetWindowPositions() {
        panel.x = restingX
        panel.y = restingY
        mediaPanelWindow.x = mediaRestingX
        mediaPanelWindow.y = mediaRestingY
    }

    function repositionWindows() {
        if (!UserSettings.panelAnimationsEnabled) {
            if (isAnimatingOut || hideAnimation.running) {
                hidePanelImmediately()
            } else {
                clearPanelAnimationState()
                positionWindowsAtTarget()
            }
            return
        }

        if (isAnimatingIn && !showAnimation.running) {
            // The visible window can finish laying out before the staged show
            // starts. Keep it offscreen until that pass consumes the final size.
            pendingShowGeometryUpdate = true
            return
        }

        const wasAnimatingIn = showAnimation.running
        const wasAnimatingOut = hideAnimation.running
        const currentPanelX = panel.x
        const currentPanelY = panel.y
        const currentMediaX = mediaPanelWindow.x
        const currentMediaY = mediaPanelWindow.y

        if (wasAnimatingIn) {
            showAnimation.stop()
        } else if (wasAnimatingOut) {
            hideAnimation.stop()
        }

        positionWindowsAtTarget()

        if (!wasAnimatingIn && !wasAnimatingOut) {
            return
        }

        if (animationProperty() === "x") {
            panel.x = currentPanelX
            panel.y = restingY
            mediaPanelWindow.x = currentMediaX
            mediaPanelWindow.y = mediaRestingY
        } else {
            panel.x = restingX
            panel.y = currentPanelY
            mediaPanelWindow.x = mediaRestingX
            mediaPanelWindow.y = currentMediaY
        }

        if (wasAnimatingIn) {
            startAnimation()
        } else {
            configureHideAnimation()
            hideAnimation.start()
        }
    }

    function setInitialWindowPositions() {
        resetWindowPositions()
        const verticalDistance = panel.height
                + (mediaPanelWindow.available ? mediaPanelWindow.height + panelGap : 0)
                + UserSettings.yAxisMargin
        const horizontalDistance = panel.width + UserSettings.xAxisMargin

        switch (panel.taskbarPos) {
        case "top":
            panel.y -= verticalDistance
            mediaPanelWindow.y -= verticalDistance
            break
        case "bottom":
            panel.y += verticalDistance
            mediaPanelWindow.y += verticalDistance
            break
        case "left":
            panel.x -= horizontalDistance
            mediaPanelWindow.x -= horizontalDistance
            break
        case "right":
            panel.x += horizontalDistance
            mediaPanelWindow.x += horizontalDistance
            break
        default:
            panel.y += verticalDistance
            mediaPanelWindow.y += verticalDistance
            break
        }
    }

    function startAnimation() {
        if (!UserSettings.panelAnimationsEnabled) {
            showPanelImmediately(false)
            return
        }
        if (!isAnimatingIn) return

        const propertyName = animationProperty()
        mainShowAnimation.property = propertyName
        mainShowAnimation.from = propertyName === "x" ? panel.x : panel.y
        mainShowAnimation.to = propertyName === "x" ? restingX : restingY
        mediaShowAnimation.property = propertyName
        mediaShowAnimation.from = propertyName === "x" ? mediaPanelWindow.x : mediaPanelWindow.y
        mediaShowAnimation.to = propertyName === "x" ? mediaRestingX : mediaRestingY
        showAnimation.start()
    }

    function hidePanel() {
        if (!UserSettings.panelAnimationsEnabled) {
            hidePanelImmediately()
            return
        }

        if (isAnimatingOut) {
            return
        }

        if (isAnimatingIn) {
            showAnimation.stop()
            isAnimatingIn = false

            const adjustedDuration = Math.max(50, Math.round(showAnimationProgress() * 300))
            setHideDuration(adjustedDuration)
        } else {
            setHideDuration(300)
        }

        closeAllMenusAndCollapse()
        startHideAnimation()
    }

    function hidePanelForFocusLoss() {
        if (!visible) {
            return
        }

        suppressNextTrayShow = true
        focusLossTrayGuardTimer.restart()
        hidePanel()
    }

    function closeAllMenusAndCollapse() {
        var i, item, j, child

        if (executableRenameContextMenu.visible) {
            executableRenameContextMenu.close()
        }
        outputDevicesRect.closeContextMenus()
        inputDevicesRect.closeContextMenus()

        for (i = 0; i < appRepeater.count; ++i) {
            item = appRepeater.itemAt(i)
            if (item && item.children) {
                for (j = 0; j < item.children.length; ++j) {
                    child = item.children[j]
                    if (child && child.hasOwnProperty('closeContextMenus')) {
                        child.closeContextMenus()
                    }
                }
            }
        }

        outputDevicesRect.expanded = false
        inputDevicesRect.expanded = false

        for (i = 0; i < appRepeater.count; ++i) {
            item = appRepeater.itemAt(i)
            if (item && item.children) {
                for (j = 0; j < item.children.length; ++j) {
                    child = item.children[j]
                    if (child && child.hasOwnProperty('expanded')) {
                        child.expanded = false
                    }
                }
            }
        }

        panelFooter.closePowerMenu()
    }

    function startHideAnimation() {
        if (!UserSettings.panelAnimationsEnabled) {
            hidePanelImmediately()
            return
        }

        isAnimatingOut = true
        configureHideAnimation()
        hideAnimation.start()
    }

    function setHideDuration(duration) {
        mainHideAnimation.duration = duration
        mediaHideAnimation.duration = duration
    }

    function configureHideAnimation() {
        const propertyName = animationProperty()
        const verticalDistance = panel.height
                + (mediaPanelWindow.available ? mediaPanelWindow.height + panelGap : 0)
                + UserSettings.yAxisMargin
        const horizontalDistance = panel.width + UserSettings.xAxisMargin

        mainHideAnimation.property = propertyName
        mainHideAnimation.from = propertyName === "x" ? panel.x : panel.y
        mediaHideAnimation.property = propertyName
        mediaHideAnimation.from = propertyName === "x" ? mediaPanelWindow.x : mediaPanelWindow.y

        switch (panel.taskbarPos) {
        case "top":
            mainHideAnimation.to = restingY - verticalDistance
            mediaHideAnimation.to = mediaRestingY - verticalDistance
            break
        case "bottom":
            mainHideAnimation.to = restingY + verticalDistance
            mediaHideAnimation.to = mediaRestingY + verticalDistance
            break
        case "left":
            mainHideAnimation.to = restingX - horizontalDistance
            mediaHideAnimation.to = mediaRestingX - horizontalDistance
            break
        case "right":
            mainHideAnimation.to = restingX + horizontalDistance
            mediaHideAnimation.to = mediaRestingX + horizontalDistance
            break
        default:
            mainHideAnimation.to = restingY + verticalDistance
            mediaHideAnimation.to = mediaRestingY + verticalDistance
            break
        }
    }

    function handleMediaAvailabilityChanged() {
        if (!visible) {
            mediaPanelWindow.visible = false
            return
        }

        mediaPanelWindow.visible = mediaPanelWindow.available
        repositionWindows()
    }

    function shouldShowSeparator(currentLayoutIndex) {
        const visibilities = [
                               UserSettings.enableDeviceManager,
                               UserSettings.enableApplicationMixer && AudioBridge.isReady && AudioBridge.applications.rowCount() > 0,
                               UserSettings.activateChatmix,
                               UserSettings.allowBrightnessControl && MonitorManager.monitorDetected
                           ]
        if (!visibilities[currentLayoutIndex]) return false
        for (let i = 0; i < currentLayoutIndex; i++) {
            if (visibilities[i]) return true
        }
        return false
    }

    Connections {
        target: KeyboardShortcutManager
        function onRegistrationFailed(message) {
            systemTray.showMessage(qsTr("Shortcut registration failed"), message)
        }
        function onSaveFailed(message) {
            systemTray.showMessage(qsTr("Settings could not be saved"), message)
        }
    }

    Connections {
        target: UserSettings
        function onSaveFailed(message) {
            systemTray.showMessage(qsTr("Settings could not be saved"), message)
        }
    }

    Connections {
        target: PowerBridge
        function onOperationFailed(message) {
            systemTray.showMessage(qsTr("Power action failed"), message)
        }
    }

    Connections {
        target: HeadsetControlBridge
        function onLowHeadsetBattery() {
            LogManager.warn("HeadsetControl", "Low headset battery detected at " + HeadsetControlBridge.batteryLevel + "%")
            systemTray.showMessage(qsTr("Low Battery"), qsTr("Headset battery is getting low"))
        }
    }

    Connections {
        target: Updater
        function onUpdateAvailableNotification(version) {
            systemTray.showMessage(
                        qsTr("Update Available"),
                        qsTr("Version %1 is available for download").arg(version),
                        Platform.SystemTrayIcon.Information,
                        3000
                        )
        }
    }

    SettingsWindow {
        id: settingsWindow
    }

    Item {
        id: cont
        anchors.bottom: UserSettings.panelPosition === 0 ? undefined : parent.bottom
        anchors.top: UserSettings.panelPosition === 0 ? parent.top : undefined
        anchors.right: parent.right
        anchors.left: parent.left

        height: panel.height

        GridLayout {
            id: mainGrid
            anchors.fill: parent
            columns: 1
            rows: 1
            columnSpacing: 0
            rowSpacing: 0

            Item {
                id: contentContainer
                clip: true
                Layout.row: 0
                Layout.column: 0
                Layout.fillHeight: true
                Layout.preferredWidth: 360

                Flickable {
                    id: contentFlickable
                    anchors.top: parent.top
                    anchors.bottom: parent.bottom
                    anchors.left: parent.left
                    anchors.right: parent.right
                    clip: true
                    contentWidth: width
                    property real visualBottomInset: -Math.min(0, panelFooter.Layout.bottomMargin)
                    contentHeight: mainLayout.y + mainLayout.implicitHeight + visualBottomInset
                    boundsBehavior: Flickable.StopAtBounds
                    interactive: contentHeight > height

                    Rectangle {
                        anchors.fill: mainLayout
                        anchors.margins: -15
                        color: panel.nativeBackdropActive ? "transparent" : Constants.panelColor
                        radius: 12
                        Rectangle {
                            anchors.fill: parent
                            color: "#00000000"
                            radius: 12
                            border.width: 1
                            border.color: "#E3E3E3"
                            opacity: 0.15
                        }
                    }

                    ColumnLayout {
                        id: mainLayout
                        x: 15
                        y: 15
                        width: contentFlickable.width - 30
                        spacing: 10
                        opacity: 0

                        Behavior on opacity {
                            enabled: UserSettings.panelAnimationsEnabled
                            NumberAnimation {
                                id: mainOpacityAnimation
                                duration: 400
                                easing.type: Easing.OutQuad
                            }
                        }

                    ColumnLayout {
                        id: deviceLayout
                        spacing: 5
                        visible: UserSettings.enableDeviceManager

                        RowLayout {
                            Layout.fillWidth: true
                            Layout.preferredHeight: 40
                            spacing: 0
                            NFToolButton {
                                Layout.preferredHeight: 40
                                Layout.preferredWidth: 40
                                flat: true
                                icon.source: volumeIcon
                                onClicked: AudioBridge.setOutputMute(!AudioBridge.outputMuted)
                                property string volumeIcon: {
                                    if (AudioBridge.outputMuted || AudioBridge.outputVolume === 0) {
                                        return "qrc:/icons/panel_volume_0.svg"
                                    } else if (AudioBridge.outputVolume <= 33) {
                                        return "qrc:/icons/panel_volume_33.svg"
                                    } else if (AudioBridge.outputVolume <= 66) {
                                        return "qrc:/icons/panel_volume_66.svg"
                                    } else {
                                        return "qrc:/icons/panel_volume_100.svg"
                                    }
                                }
                            }

                            ColumnLayout {
                                spacing: -4
                                Label {
                                    opacity: 1
                                    elide: Text.ElideRight
                                    Layout.preferredWidth: outputSlider.implicitWidth - 30
                                    Layout.leftMargin: 18
                                    Layout.rightMargin: 25
                                    text: AudioBridge.outputDeviceDisplayName
                                }

                                ProgressSlider {
                                    id: outputSlider
                                    value: pressed ? value : AudioBridge.outputVolume
                                    from: 0
                                    to: 100
                                    Layout.fillWidth: true
                                    audioLevel: AudioBridge.outputAudioLevel
                                    onValueChanged: {
                                        if (pressed) {
                                            AudioBridge.setOutputVolume(value)
                                        }
                                    }
                                    onPressedChanged: {
                                        if (!pressed) {
                                            AudioBridge.setOutputVolume(value)
                                            Utils.playFeedbackSound()
                                        }
                                    }
                                    onWheelChanged: {
                                        AudioBridge.setOutputVolume(value)
                                    }
                                }
                            }

                            NFToolButton {
                                icon.source: "qrc:/icons/arrow.svg"
                                rotation: outputDevicesRect.expanded ? 90 : 0
                                visible: AudioBridge.isReady && AudioBridge.outputDevices.count > 1
                                Layout.preferredHeight: 35
                                Layout.preferredWidth: 35
                                onClicked: outputDevicesRect.expanded = !outputDevicesRect.expanded
                                Behavior on rotation {
                                    NumberAnimation {
                                        duration: 150
                                        easing.type: Easing.Linear
                                    }
                                }
                            }
                        }

                        DevicesListView {
                            id: outputDevicesRect
                            model: AudioBridge.outputDevices
                            nativeBackdropActive: panel.nativeBackdropActive
                            onDeviceClicked: function(name, index) {
                                AudioBridge.setOutputDevice(index)
                                expanded = false
                            }
                        }

                        RowLayout {
                            Layout.fillWidth: true
                            Layout.preferredHeight: 40
                            spacing: 0
                            NFToolButton {
                                Layout.preferredWidth: 40
                                Layout.preferredHeight: 40
                                flat: true
                                icon.source: AudioBridge.inputMuted ? "qrc:/icons/mic_muted.svg" : "qrc:/icons/mic.svg"
                                icon.height: 16
                                icon.width: 16
                                onClicked: AudioBridge.setInputMute(!AudioBridge.inputMuted)
                            }

                            ColumnLayout {
                                spacing: -4
                                Label {
                                    opacity: 1
                                    elide: Text.ElideRight
                                    Layout.preferredWidth: inputSlider.implicitWidth - 30
                                    Layout.leftMargin: 18
                                    Layout.rightMargin: 25
                                    text: AudioBridge.inputDeviceDisplayName
                                }

                                ProgressSlider {
                                    id: inputSlider
                                    value: pressed ? value : AudioBridge.inputVolume
                                    from: 0
                                    to: 100
                                    audioLevel: AudioBridge.inputAudioLevel
                                    Layout.fillWidth: true
                                    onValueChanged: {
                                        if (pressed) {
                                            AudioBridge.setInputVolume(value)
                                        }
                                    }
                                    onPressedChanged: {
                                        if (!pressed) {
                                            AudioBridge.setInputVolume(value)
                                        }
                                    }
                                    onWheelChanged: {
                                        AudioBridge.setInputVolume(value)
                                    }
                                }
                            }

                            NFToolButton {
                                icon.source: "qrc:/icons/arrow.svg"
                                rotation: inputDevicesRect.expanded ? 90 : 0
                                Layout.preferredHeight: 35
                                Layout.preferredWidth: 35
                                visible: AudioBridge.isReady && AudioBridge.inputDevices.count > 1
                                onClicked: inputDevicesRect.expanded = !inputDevicesRect.expanded
                                Behavior on rotation {
                                    NumberAnimation {
                                        duration: 150
                                        easing.type: Easing.Linear
                                    }
                                }
                            }
                        }

                        DevicesListView {
                            id: inputDevicesRect
                            model: AudioBridge.inputDevices
                            nativeBackdropActive: panel.nativeBackdropActive
                            onDeviceClicked: function(name, index) {
                                AudioBridge.setInputDevice(index)
                                expanded = false
                            }
                        }
                    }

                    Rectangle {
                        id: deviceLytSeparator
                        Layout.preferredHeight: 1
                        Layout.fillWidth: true
                        color: Constants.separatorColor
                        opacity: 0.15
                        visible: panel.shouldShowSeparator(1)
                        Layout.rightMargin: -14
                        Layout.leftMargin: -14
                    }

                    ColumnLayout {
                        id: appLayout
                        spacing: 5
                        visible: UserSettings.enableApplicationMixer && AudioBridge.isReady && AudioBridge.applications.rowCount() > 0
                        Layout.fillWidth: true

                        Repeater {
                            id: appRepeater
                            model: AudioBridge.groupedApplications
                            delegate: ColumnLayout {
                                id: appDelegateRoot
                                spacing: 5
                                Layout.fillWidth: true
                                required property var model
                                required property int index
                                readonly property real applicationListHeight: individualAppsRect.expandedNeededHeight

                                RowLayout {
                                    Layout.preferredHeight: 40
                                    Layout.fillWidth: true
                                    spacing: 0

                                    NFToolButton {
                                        id: executableMuteButton
                                        Layout.preferredWidth: 40
                                        Layout.preferredHeight: 40
                                        flat: !checked
                                        checkable: true
                                        highlighted: checked
                                        checked: appDelegateRoot.model.allMuted
                                        ToolTip.text: appDelegateRoot.model.displayName
                                        ToolTip.visible: hovered
                                        ToolTip.delay: 1000
                                        opacity: highlighted ? 0.3 : (enabled ? 1 : 0.5)
                                        icon.color: "transparent"
                                        icon.source: appDelegateRoot.model.isSystemSounds ? Constants.systemIcon : appDelegateRoot.model.iconPath
                                        onClicked: AudioBridge.setExecutableMute(appDelegateRoot.model.executableName, checked)
                                        Component.onCompleted: palette.accent = palette.button
                                    }

                                    ColumnLayout {
                                        spacing: -4

                                        Label {
                                            opacity: panel.chatMixEffectiveEnabled ? 0.3 : 1
                                            elide: Text.ElideRight
                                            Layout.preferredWidth: 200
                                            Layout.leftMargin: 18
                                            Layout.rightMargin: 25
                                            text: {
                                                let name = appDelegateRoot.model.displayName
                                                if (appDelegateRoot.model.isSystemSounds) {
                                                    name = qsTr("System sounds")
                                                }

                                                if (panel.chatMixEffectiveEnabled && AudioBridge.isCommApp(name) && !appDelegateRoot.model.isSystemSounds) {
                                                    name += " (Comm)"
                                                }
                                                return name
                                            }

                                            MouseArea {
                                                anchors.fill: parent
                                                acceptedButtons: Qt.RightButton
                                                onClicked: function(mouse) {
                                                    if (mouse.button === Qt.RightButton && !appDelegateRoot.model.isSystemSounds) {
                                                        executableRenameContextMenu.originalName = appDelegateRoot.model.executableName
                                                        executableRenameContextMenu.currentCustomName = AudioBridge.getCustomExecutableName(appDelegateRoot.model.executableName)
                                                        executableRenameContextMenu.popup()
                                                    }
                                                }
                                            }
                                        }

                                        ProgressSlider {
                                            onActiveFocusChanged: focus = false
                                            id: executableVolumeSlider
                                            from: 0
                                            to: 100
                                            value: pressed ? value : appDelegateRoot.model.averageVolume
                                            enabled: !panel.chatMixEffectiveEnabled && !executableMuteButton.highlighted
                                            opacity: enabled ? 1 : 0.5
                                            Layout.fillWidth: true
                                            displayProgress: !appDelegateRoot.model.isSystemSounds
                                            audioLevel: {
                                                // Keep this binding subscribed even while the session list is collapsed.
                                                AudioBridge.applicationAudioLevels
                                                return !appDelegateRoot.model.isSystemSounds
                                                    ? (appDelegateRoot.model.averageAudioLevel || 0)
                                                    : 0
                                            }
                                            onValueChanged: {
                                                if (!panel.chatMixEffectiveEnabled && pressed) {
                                                    AudioBridge.setExecutableVolume(appDelegateRoot.model.executableName, value)
                                                }
                                            }
                                            onPressedChanged: {
                                                if (!pressed && !panel.chatMixEffectiveEnabled) {
                                                    AudioBridge.setExecutableVolume(appDelegateRoot.model.executableName, value)
                                                }
                                            }
                                            onWheelChanged: {
                                                if (!panel.chatMixEffectiveEnabled) {
                                                    AudioBridge.setExecutableVolume(appDelegateRoot.model.executableName, value)
                                                }
                                            }
                                        }
                                    }

                                    NFToolButton {
                                        onActiveFocusChanged: focus = false
                                        icon.source: "qrc:/icons/arrow.svg"
                                        rotation: individualAppsRect.expanded ? 90 : 0
                                        visible: appDelegateRoot.model.sessionCount > 1
                                        Layout.preferredHeight: 35
                                        Layout.preferredWidth: 35
                                        onClicked: individualAppsRect.expanded = !individualAppsRect.expanded

                                        Behavior on rotation {
                                            NumberAnimation {
                                                duration: 150
                                                easing.type: Easing.Linear
                                            }
                                        }
                                    }
                                }

                                ApplicationsListView {
                                    id: individualAppsRect
                                    model: AudioBridge.getSessionsForExecutable(appDelegateRoot.model.executableName)
                                    executableName: appDelegateRoot.model.executableName
                                    nativeBackdropActive: panel.nativeBackdropActive

                                    onApplicationVolumeChanged: function(appId, volume) {
                                        AudioBridge.setApplicationVolume(appId, volume)
                                    }

                                    onApplicationMuteChanged: function(appId, muted) {
                                        AudioBridge.setApplicationMute(appId, muted)
                                    }
                                }
                            }
                        }
                    }

                    Rectangle {
                        id: appsLytSeparator
                        visible: panel.shouldShowSeparator(2)
                        Layout.preferredHeight: 1
                        Layout.fillWidth: true
                        color: Constants.separatorColor
                        opacity: 0.15
                        Layout.rightMargin: -14
                        Layout.leftMargin: -14
                    }

                    ColumnLayout {
                        visible: UserSettings.activateChatmix
                        id: chatMixLayout
                        spacing: 5

                        RowLayout {
                            Layout.fillWidth: true
                            Layout.preferredHeight: 40
                            spacing: 0

                            NFToolButton {
                                Layout.preferredWidth: 40
                                Layout.preferredHeight: 40
                                icon.width: 15
                                icon.height: 15
                                icon.source: "qrc:/icons/headset.svg"
                                icon.color: palette.text
                                checkable: true
                                checked: !UserSettings.chatMixEnabled
                                opacity: checked ? 0.3 : 1
                                Component.onCompleted: palette.accent = palette.button
                                onClicked: {
                                    const wasEnabled = UserSettings.activateChatmix && UserSettings.chatMixEnabled
                                    UserSettings.chatMixEnabled = !checked
                                    checked = Qt.binding(function() { return !UserSettings.chatMixEnabled })
                                    const isEnabled = UserSettings.activateChatmix && UserSettings.chatMixEnabled
                                    if (wasEnabled === isEnabled) {
                                        return
                                    }

                                    if (isEnabled) {
                                        AudioBridge.applyChatMixToApplications(UserSettings.chatMixValue)
                                    } else {
                                        AudioBridge.restoreOriginalVolumes()
                                    }
                                }
                            }

                            ColumnLayout {
                                spacing: -4

                                Label {
                                    opacity: 1
                                    text: qsTr("ChatMix")
                                    Layout.leftMargin: 18
                                    Layout.rightMargin: 25
                                }

                                NFSlider {
                                    id: chatMixSlider
                                    value: UserSettings.chatMixValue
                                    from: 0
                                    to: 100
                                    Layout.fillWidth: true
                                    enabled: UserSettings.chatMixEnabled

                                    ToolTip {
                                        parent: chatMixSlider.handle
                                        visible: chatMixSlider.pressed || chatMixSlider.hovered
                                        delay: chatMixSlider.pressed ? 0 : 1000
                                        text: Math.round(chatMixSlider.value).toString()
                                    }

                                    function saveVolume() {
                                        const previousValue = UserSettings.chatMixValue
                                        UserSettings.chatMixValue = Math.round(value)
                                        value = Qt.binding(function() { return UserSettings.chatMixValue })
                                        if (UserSettings.activateChatmix && UserSettings.chatMixEnabled
                                                && UserSettings.chatMixValue !== previousValue) {
                                            AudioBridge.applyChatMixToApplications(UserSettings.chatMixValue)
                                        }
                                    }
                                    onMoved: saveVolume()
                                    onWheelChanged: saveVolume()
                                }
                            }

                            IconImage {
                                Layout.preferredWidth: 35
                                Layout.preferredHeight: 35
                                sourceSize.width: 15
                                sourceSize.height: 15
                                color: palette.text
                                source: "qrc:/icons/music.svg"
                                enabled: UserSettings.chatMixEnabled
                            }
                        }
                    }

                    Rectangle {
                        id: chatMixLytSeparator
                        visible: panel.shouldShowSeparator(3)
                        Layout.preferredHeight: 1
                        Layout.fillWidth: true
                        color: Constants.separatorColor
                        opacity: 0.15
                        Layout.rightMargin: -14
                        Layout.leftMargin: -14
                    }

                    ColumnLayout {
                        visible: UserSettings.allowBrightnessControl && MonitorManager.monitorDetected
                        id: brightnessLayout
                        spacing: 5

                        RowLayout {
                            Layout.fillWidth: true
                            Layout.preferredHeight: 40
                            spacing: 0

                            Item {
                                id: nightLightControl
                                Layout.preferredWidth: 40
                                Layout.preferredHeight: 40
                                readonly property string helpText: !MonitorManager.nightLightSupported
                                    ? qsTr("Night Light control is unavailable. Brightness can still be adjusted.")
                                    : MonitorManager.nightLightEnabled ? qsTr("Turn Night Light off") : qsTr("Turn Night Light on")

                                // Keep hover help available when the Night Light button is disabled.
                                HoverHandler {
                                    id: nightLightHover
                                }
                                ToolTip.visible: nightLightHover.hovered
                                ToolTip.delay: 1000
                                ToolTip.text: helpText

                                NFToolButton {
                                    anchors.fill: parent
                                    enabled: MonitorManager.nightLightSupported
                                    onClicked: MonitorManager.toggleNightLight()
                                    icon.source: MonitorManager.nightLightEnabled ? "qrc:/icons/nightlight.svg" : "qrc:/icons/brightness.svg"
                                    icon.width: 22
                                    icon.height: 22
                                    Accessible.name: nightLightControl.helpText
                                }
                            }

                            ColumnLayout {
                                spacing: -4

                                Label {
                                    opacity: 1
                                    text: qsTr("Brightness")
                                    Layout.leftMargin: 18
                                    Layout.rightMargin: 25
                                }

                                NFSlider {
                                    id: brightnessSlider
                                    from: 0
                                    to: 100
                                    value: UserSettings.ddcciBrightness
                                    Layout.fillWidth: true
                                    function saveBrightness() {
                                        const requestedValue = Math.round(value)
                                        UserSettings.ddcciBrightness = requestedValue
                                        value = Qt.binding(function() { return UserSettings.ddcciBrightness })
                                        if (UserSettings.ddcciBrightness !== requestedValue) {
                                            return
                                        }
                                        MonitorManager.setWMIBrightness(UserSettings.ddcciBrightness)
                                        MonitorManager.setDDCCIBrightness(UserSettings.ddcciBrightness, UserSettings.ddcciQueueDelay)
                                    }
                                    onMoved: saveBrightness()
                                    onWheelChanged: saveBrightness()

                                    ToolTip {
                                        parent: brightnessSlider.handle
                                        visible: brightnessSlider.pressed || brightnessSlider.hovered
                                        delay: brightnessSlider.pressed ? 0 : 1000
                                        text: Math.round(brightnessSlider.value).toString()
                                    }
                                }
                            }
                        }
                    }

                        PanelFooter {
                            id: panelFooter
                            nativeBackdropActive: panel.nativeBackdropActive
                            Layout.fillWidth: true
                            Layout.fillHeight: false
                            Layout.preferredHeight: 50
                            Layout.leftMargin: -14
                            Layout.rightMargin: -14
                            Layout.bottomMargin: -14
                            onHidePanel: panel.hidePanel()
                            onShowSettingsWindow: settingsWindow.showPreferredPane()
                            onShowUpdatePane: settingsWindow.showUpdatePane()
                            onShowPowerConfirmationWindow: function(action) {
                                powerConfirmationWindow.setAction(action)
                                powerConfirmationWindow.show()
                            }
                            onShowHeadsetcontrolPane: {
                                panel.hidePanel()
                                settingsWindow.showHeadsetcontrolPane()
                            }
                        }
                    }
                }
            }

        }
    }

    ExecutableRenameContextMenu {
        id: executableRenameContextMenu
    }

    ExecutableRenameDialog {
        id: executableRenameDialog
        anchors.centerIn: parent
        originalName: executableRenameContextMenu.originalName
        onOpened: {
            setNameFieldText(executableRenameContextMenu.currentCustomName)
        }
    }
}
