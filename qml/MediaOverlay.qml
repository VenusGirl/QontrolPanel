import QtQuick
import QtQuick.Layouts
import QtQuick.Controls.FluentWinUI3
import QtQuick.Controls.impl
import QtQuick.Window
import ChrisLauinger77.QontrolPanel

ApplicationWindow {
    id: mediaOverlayWindow
    width: calculateWidth()
    height: calculateHeight()
    visible: false
    flags: Qt.FramelessWindowHint | Qt.WindowStaysOnTopHint | Qt.WindowDoesNotAcceptFocus
    color: "#00000000"

    property bool isAnimatingIn: false
    property bool isAnimatingOut: false
    property string previousTitle: ""
    property string previousArtist: ""
    property bool hasReceivedFirstUpdate: false
    property bool suppressNextMediaInfoOverlay: false
    property bool nativeBackdropActive: false
    property real restingX: 0
    property real restingY: 0

    transientParent: null

    function calculateWidth() {
        // Size multipliers: tiny=0.8, normal=1.0, big=1.2
        const sizeMultipliers = [0.8, 1.0, 1.2]
        const multiplier = sizeMultipliers[UserSettings.mediaOverlaySize] || 1.0
        return Math.round(300 * multiplier)
    }

    function calculateHeight() {
        const sizeMultipliers = [0.8, 1.0, 1.2]
        const multiplier = sizeMultipliers[UserSettings.mediaOverlaySize] || 1.0
        return Math.round(80 * multiplier) + 24
    }

    Component.onCompleted: {
        rememberCurrentMedia()
        positionWindow()
        Qt.callLater(updateNativeBackdrop)
    }

    function rememberCurrentMedia() {
        previousTitle = MediaSessionBridge.mediaTitle || ""
        previousArtist = MediaSessionBridge.mediaArtist || ""
        hasReceivedFirstUpdate = hasReceivedFirstUpdate || previousTitle !== ""
    }

    function updateNativeBackdrop() {
        nativeBackdropActive = WindowsBackdrop.applyTransientBackdrop(mediaOverlayWindow)
    }

    Connections {
        target: Qt.application.styleHints

        function onColorSchemeChanged() {
            mediaOverlayWindow.updateNativeBackdrop()
        }
    }

    Connections {
        target: UserSettings

        function onEnableMediaOverlayChanged() {
            rememberCurrentMedia()
        }

        function onMediaOverlayPositionChanged() {
            repositionWindow()
        }

        function onMediaOverlaySizeChanged() {
            width = calculateWidth()
            height = calculateHeight()
            repositionWindow()
        }
    }

    Connections {
        target: MediaSessionBridge

        function onMediaSourceSwitchRequested() {
            suppressNextMediaInfoOverlay = true
            sourceSwitchSuppressionTimer.restart()
        }

        function onMediaInfoChanged() {
            const newTitle = MediaSessionBridge.mediaTitle || ""
            const newArtist = MediaSessionBridge.mediaArtist || ""
            const songChanged = newTitle !== previousTitle || newArtist !== previousArtist
            const hadPreviousUpdate = hasReceivedFirstUpdate

            // Keep the baseline current even while notifications are disabled.
            previousTitle = newTitle
            previousArtist = newArtist
            hasReceivedFirstUpdate = true

            if (suppressNextMediaInfoOverlay) {
                suppressNextMediaInfoOverlay = false
                sourceSwitchSuppressionTimer.stop()
                return
            }

            if (!UserSettings.enableMediaOverlay || !UserSettings.enableMediaSessionManager
                    || !hadPreviousUpdate) {
                return
            }

            // Only show overlay if song changed (title or artist is different)
            if (newTitle !== "" && songChanged) {
                showOverlay()
            }
        }
    }

    Timer {
        id: sourceSwitchSuppressionTimer
        interval: 2000
        repeat: false
        onTriggered: suppressNextMediaInfoOverlay = false
    }

    Timer {
        id: autoHideTimer
        interval: 3000
        repeat: false
        onTriggered: mediaOverlayWindow.hideOverlay()
    }

    PropertyAnimation {
        id: showAnimation
        target: mediaOverlayWindow
        duration: 300
        easing.type: Easing.OutQuad
        onStarted: {
            mediaOverlayWindow.isAnimatingIn = true
            if (mediaOverlayWindow.nativeBackdropActive) {
                overlayRect.opacity = 1
            } else if (!contentOpacityAnimation.running && overlayRect.opacity < 1) {
                contentOpacityAnimation.from = overlayRect.opacity
                contentOpacityAnimation.start()
            }
        }
        onFinished: {
            mediaOverlayWindow.isAnimatingIn = false
            autoHideTimer.start()
        }
    }

    PropertyAnimation {
        id: hideAnimation
        target: mediaOverlayWindow
        duration: 250
        easing.type: Easing.OutQuad
        onStarted: {
            mediaOverlayWindow.isAnimatingOut = true
            if (!mediaOverlayWindow.nativeBackdropActive
                    && !hideOpacityAnimation.running && overlayRect.opacity > 0) {
                hideOpacityAnimation.from = overlayRect.opacity
                hideOpacityAnimation.start()
            }
        }
        onFinished: {
            mediaOverlayWindow.visible = false
            mediaOverlayWindow.isAnimatingOut = false
            resetWindowPosition()
        }
    }

    PropertyAnimation {
        id: contentOpacityAnimation
        target: overlayRect
        property: "opacity"
        duration: 200
        easing.type: Easing.OutQuad
        from: 0
        to: 1
    }

    PropertyAnimation {
        id: hideOpacityAnimation
        target: overlayRect
        property: "opacity"
        duration: 150
        easing.type: Easing.InQuad
        from: 1
        to: 0
    }

    function showOverlay() {
        if ((visible && !isAnimatingOut) || isAnimatingIn) {
            autoHideTimer.restart()
            return
        }

        if (isAnimatingOut) {
            const currentX = x
            const currentY = y
            hideAnimation.stop()
            hideOpacityAnimation.stop()
            isAnimatingOut = false

            animateFrom(currentX, currentY)
            return
        }

        positionWindow()
        setInitialWindowPosition()
        overlayRect.opacity = nativeBackdropActive ? 1 : 0

        visible = true
        animateFrom(x, y)
    }

    function hideOverlay() {
        if (isAnimatingOut) {
            return
        }

        if (isAnimatingIn) {
            showAnimation.stop()
            contentOpacityAnimation.stop()
            isAnimatingIn = false
        }

        autoHideTimer.stop()
        animateToHide()
        hideAnimation.start()
    }

    function positionWindow() {
        const screenWidth = Utils.getAvailableDesktopWidth()
        const screenHeight = Utils.getAvailableDesktopHeight()
        const taskbarOffset = UserSettings.taskbarOffset
        const margin = 12

        // Position mapping: 0=top-left, 1=top-center, 2=top-right, 3=left, 4=right, 5=bottom-left, 6=bottom-center, 7=bottom-right
        switch (UserSettings.mediaOverlayPosition) {
        case 0: // top-left
            x = margin
            y = taskbarOffset + margin
            break
        case 1: // top-center
            x = (screenWidth - width) / 2
            y = taskbarOffset + margin
            break
        case 2: // top-right
            x = screenWidth - width - margin
            y = taskbarOffset + margin
            break
        case 3: // left
            x = margin
            y = (screenHeight - height) / 2
            break
        case 4: // right
            x = screenWidth - width - margin
            y = (screenHeight - height) / 2
            break
        case 5: // bottom-left
            x = margin
            y = screenHeight - height - taskbarOffset - margin
            break
        case 6: // bottom-center
            x = (screenWidth - width) / 2
            y = screenHeight - height - taskbarOffset - margin
            break
        case 7: // bottom-right
            x = screenWidth - width - margin
            y = screenHeight - height - taskbarOffset - margin
            break
        default: // fallback to top-center
            x = (screenWidth - width) / 2
            y = taskbarOffset + margin
            break
        }

        restingX = x
        restingY = y
    }

    function repositionWindow() {
        const wasAnimatingIn = isAnimatingIn
        const wasAnimatingOut = isAnimatingOut
        const currentX = x
        const currentY = y

        if (wasAnimatingIn) {
            showAnimation.stop()
        } else if (wasAnimatingOut) {
            hideAnimation.stop()
        }

        positionWindow()

        if (!wasAnimatingIn && !wasAnimatingOut) {
            return
        }

        if (getAnimationProperty() === "x") {
            x = currentX
            y = restingY
        } else {
            x = restingX
            y = currentY
        }

        if (wasAnimatingIn) {
            animateFrom(x, y)
        } else {
            animateToHide()
            hideAnimation.start()
        }
    }

    function setInitialWindowPosition() {
        x = restingX
        y = restingY

        switch (UserSettings.mediaOverlayPosition) {
        case 0: // top-left
        case 1: // top-center
        case 2: // top-right
            y = restingY - height
            break
        case 3: // left
            x = restingX - width
            break
        case 4: // right
            x = restingX + width
            break
        case 5: // bottom-left
        case 6: // bottom-center
        case 7: // bottom-right
            y = restingY + height
            break
        }
    }

    function animateFrom(fromX, fromY) {
        showAnimation.property = getAnimationProperty()
        showAnimation.from = showAnimation.property === "x" ? fromX : fromY
        showAnimation.to = showAnimation.property === "x" ? restingX : restingY
        showAnimation.start()
    }

    function animateToHide() {
        hideAnimation.property = getAnimationProperty()
        hideAnimation.from = hideAnimation.property === "x" ? x : y
        hideAnimation.to = getAnimationTarget()
    }

    function getAnimationProperty() {
        switch (UserSettings.mediaOverlayPosition) {
        case 0: // top-left
        case 1: // top-center
        case 2: // top-right
        case 5: // bottom-left
        case 6: // bottom-center
        case 7: // bottom-right
            return "y"
        case 3: // left
        case 4: // right
            return "x"
        }
        return "y"
    }

    function getAnimationTarget() {
        switch (UserSettings.mediaOverlayPosition) {
        case 0: // top-left
        case 1: // top-center
        case 2: // top-right
            return restingY - height
        case 3: // left
            return restingX - width
        case 4: // right
            return restingX + width
        case 5: // bottom-left
        case 6: // bottom-center
        case 7: // bottom-right
            return restingY + height
        }
        return restingY - height
    }

    function resetWindowPosition() {
        x = restingX
        y = restingY
    }

    Rectangle {
        id: overlayRect
        anchors.fill: parent
        color: mediaOverlayWindow.nativeBackdropActive ? "transparent" : Constants.panelColor
        radius: 5
        opacity: 0

        Rectangle {
            anchors.fill: parent
            color: "#00000000"
            radius: 5
            border.width: 1
            border.color: Constants.separatorColor
            opacity: 0.15
        }

        ColumnLayout {
            anchors.fill: parent
            anchors.margins: 10
            spacing: 6

            RowLayout {
                Layout.fillWidth: true
                Layout.preferredHeight: 18
                spacing: 7

                Item {
                    Layout.preferredWidth: 16
                    Layout.preferredHeight: 16

                    Image {
                        anchors.fill: parent
                        source: MediaSessionBridge.sourceIcon || ""
                        fillMode: Image.PreserveAspectFit
                        smooth: true
                        visible: MediaSessionBridge.sourceIcon !== ""
                    }

                    IconImage {
                        anchors.fill: parent
                        source: "qrc:/icons/music.svg"
                        color: palette.text
                        opacity: 0.7
                        visible: MediaSessionBridge.sourceIcon === ""
                    }
                }

                Label {
                    text: MediaSessionBridge.sourceName || ""
                    font.pixelSize: 11
                    font.bold: true
                    elide: Text.ElideRight
                    Layout.fillWidth: true
                }
            }

            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 10

                // Album art - span 2
                Rectangle {
                    Layout.preferredWidth: parent.height
                    Layout.preferredHeight: parent.height
                    Layout.alignment: Qt.AlignLeft | Qt.AlignVCenter
                    color: "#2a2a2a"
                    radius: 3

                    Image {
                        anchors.fill: parent
                        anchors.margins: 2
                        source: MediaSessionBridge.mediaArt || ""
                        fillMode: Image.PreserveAspectCrop
                        visible: MediaSessionBridge.mediaArt !== ""
                        smooth: true
                    }

                    IconImage {
                        anchors.centerIn: parent
                        source: "qrc:/icons/headset.svg"
                        sourceSize.width: 24
                        sourceSize.height: 24
                        color: palette.text
                        opacity: 0.3
                        visible: MediaSessionBridge.mediaArt === ""
                    }
                }

                // Title and Artist - span 1
                ColumnLayout {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    spacing: 10

                    Label {
                        text: MediaSessionBridge.mediaTitle || qsTr("No media playing")
                        font.pixelSize: 13
                        font.bold: true
                        Layout.fillWidth: true
                        wrapMode: Text.Wrap
                        elide: Text.ElideRight
                        maximumLineCount: 2
                    }

                    Label {
                        text: MediaSessionBridge.mediaArtist || ""
                        font.pixelSize: 11
                        opacity: 0.7
                        Layout.fillWidth: true
                        elide: Text.ElideRight
                        maximumLineCount: 1
                        visible: MediaSessionBridge.mediaArtist !== ""
                    }
                }
            }
        }
    }
}
