pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Layouts
import QtQuick.Controls.FluentWinUI3
import ChrisLauinger77.QontrolPanel

ColumnLayout {
    id: lyt
    spacing: 3

    function applyAcceptedChatMixState(wasEnabled) {
        const isEnabled = UserSettings.activateChatmix && UserSettings.chatMixEnabled
        if (isEnabled === wasEnabled) {
            return
        }
        if (isEnabled) {
            AudioBridge.applyChatMixToApplications(UserSettings.chatMixValue)
        } else {
            AudioBridge.restoreOriginalVolumes()
        }
    }

    Label {
        Layout.fillWidth: true
        visible: AudioBridge.lastError.length > 0
        text: AudioBridge.lastError
        wrapMode: Text.Wrap
    }

    Label {
        text: qsTr("Communication Apps")
        font.pixelSize: 22
        font.bold: true
        Layout.bottomMargin: 15
    }

    CustomScrollView {
        Layout.fillWidth: true
        Layout.fillHeight: true

        Column {
            width: parent.width
            spacing: 3

            Card {
                width: parent.width
                title: qsTr("Activate ChatMix")
                description: qsTr("Show ChatMix controls")

                additionalControl: LabeledSwitch {
                    id: activateChatMixSwitch
                    checked: UserSettings.activateChatmix
                    onClicked: {
                        if (checked) {
                            chatMixWarningDialog.open();
                        } else {
                            const wasEnabled = UserSettings.activateChatmix && UserSettings.chatMixEnabled
                            UserSettings.activateChatmix = false;
                            if (!UserSettings.activateChatmix) {
                                UserSettings.chatMixEnabled = false;
                            }
                            lyt.applyAcceptedChatMixState(wasEnabled)
                        }
                        checked = Qt.binding(function() { return UserSettings.activateChatmix })
                    }
                }
            }

            Card {
                show: UserSettings.activateChatmix
                width: parent.width
                title: qsTr("Use ChatMix volume")
                description: qsTr("Control communication apps separately from other applications")

                additionalControl: LabeledSwitch {
                    checked: UserSettings.chatMixEnabled
                    onClicked: {
                        const wasEnabled = UserSettings.activateChatmix && UserSettings.chatMixEnabled
                        UserSettings.chatMixEnabled = checked;
                        checked = Qt.binding(function() { return UserSettings.chatMixEnabled })
                        lyt.applyAcceptedChatMixState(wasEnabled)
                    }
                }
            }

            Card {
                show: UserSettings.activateChatmix
                width: parent.width
                title: qsTr("ChatMix volume")
                description: qsTr("The volume to set for non communication applications when ChatMix is enabled")
                enabled: UserSettings.chatMixEnabled

                additionalControl: RowLayout {
                    spacing: 12

                    NFSlider {
                        id: chatMixVolumeSlider
                        value: UserSettings.chatMixValue
                        from: 0
                        to: 100
                        Layout.preferredWidth: 180
                        enabled: UserSettings.chatMixEnabled

                        function saveVolume() {
                            const previousValue = UserSettings.chatMixValue
                            UserSettings.chatMixValue = Math.round(value);
                            value = Qt.binding(function() { return UserSettings.chatMixValue })
                            if (UserSettings.activateChatmix && UserSettings.chatMixEnabled
                                    && UserSettings.chatMixValue !== previousValue) {
                                AudioBridge.applyChatMixToApplications(UserSettings.chatMixValue);
                            }
                        }
                        onMoved: saveVolume()
                        onWheelChanged: saveVolume()
                    }

                    Label {
                        text: Math.round(chatMixVolumeSlider.value).toString()
                        opacity: 0.7
                    }
                }
            }

            Card {
                show: UserSettings.activateChatmix
                width: parent.width
                title: qsTr("Restored volume")
                description: qsTr("The volume to set for applications when ChatMix is disabled")
                enabled: UserSettings.chatMixEnabled

                additionalControl: RowLayout {
                    spacing: 12

                    NFSlider {
                        id: chatMixRestoreVolumeSlider
                        value: UserSettings.chatmixRestoreVolume
                        from: 0
                        to: 100
                        Layout.preferredWidth: 180

                        function saveVolume() {
                            UserSettings.chatmixRestoreVolume = Math.round(value);
                            value = Qt.binding(function() { return UserSettings.chatmixRestoreVolume })
                        }
                        onMoved: saveVolume()
                        onWheelChanged: saveVolume()
                    }

                    Label {
                        text: Math.round(chatMixRestoreVolumeSlider.value).toString()
                        opacity: 0.7
                    }
                }
            }

            Card {
                show: UserSettings.activateChatmix
                width: parent.width
                title: qsTr("Communication Applications")
                description: qsTr("Add application names that should be treated as communication apps")

                additionalControl: Button {
                    text: qsTr("Add App")
                    onClicked: addAppDialog.open()
                }
            }

            Repeater {
                model: AudioBridge.commAppsList
                Card {
                    id: appCard
                    show: UserSettings.activateChatmix
                    required property var model
                    width: parent.width
                    title: model.name
                    iconSource: model.icon
                    imageMode: true
                    iconWidth: 20
                    iconHeight: 20
                    additionalControl: Button {
                        text: qsTr("Remove")
                        onClicked: {
                            AudioBridge.removeCommApp(appCard.model.name);
                        }
                    }
                }
            }
        }
    }

    Dialog {
        id: chatMixWarningDialog
        title: qsTr("Enable ChatMix Warning")
        modal: true
        width: 400
        anchors.centerIn: parent

        ColumnLayout {
            anchors.fill: parent
            spacing: 20

            Label {
                text: qsTr("Activating ChatMix will initially set all non communication application volumes to 50%. This might cause loud audio output.")
                wrapMode: Text.WordWrap
                Layout.fillWidth: true
            }

            Label {
                text: qsTr("It is recommended to lower your master volume before proceeding to avoid sudden loud sounds.")
                wrapMode: Text.WordWrap
                Layout.fillWidth: true
                font.bold: true
                color: "orange"
            }

            Label {
                Layout.fillWidth: true
                visible: UserSettings.lastError.length > 0
                text: UserSettings.lastError
                wrapMode: Text.Wrap
            }

            RowLayout {
                spacing: 15
                Layout.topMargin: 10

                Button {
                    text: qsTr("Cancel")
                    onClicked: {
                        chatMixWarningDialog.close();
                    }
                    Layout.fillWidth: true
                }

                Button {
                    text: qsTr("Activate")
                    highlighted: true
                    Layout.fillWidth: true
                    onClicked: {
                        const wasEnabled = UserSettings.activateChatmix && UserSettings.chatMixEnabled
                        UserSettings.activateChatmix = true;
                        if (!UserSettings.activateChatmix) {
                            return
                        }
                        UserSettings.chatMixEnabled = true;
                        if (!UserSettings.chatMixEnabled) {
                            return
                        }
                        lyt.applyAcceptedChatMixState(wasEnabled)
                        chatMixWarningDialog.close();
                    }
                }
            }
        }
    }

    Dialog {
        id: addAppDialog
        title: qsTr("Add Communication App")
        modal: true
        width: 350
        anchors.centerIn: parent

        ColumnLayout {
            anchors.fill: parent
            spacing: 15

            Label {
                text: qsTr("Enter application name (e.g., Discord)")
            }

            TextField {
                id: executableField
                Layout.fillWidth: true
                placeholderText: qsTr("Discord")
            }

            Label {
                Layout.fillWidth: true
                visible: AudioBridge.lastError.length > 0
                text: AudioBridge.lastError
                wrapMode: Text.Wrap
            }

            RowLayout {
                spacing: 15
                Button {
                    text: qsTr("Cancel")
                    onClicked: addAppDialog.close()
                    Layout.fillWidth: true
                }

                Button {
                    text: qsTr("Add")
                    enabled: executableField.text.length > 0
                    Layout.fillWidth: true
                    highlighted: true
                    onClicked: {
                        if (AudioBridge.addCommApp(executableField.text)) {
                            executableField.text = "";
                            addAppDialog.close();
                        }
                    }
                }
            }
        }
    }
}
